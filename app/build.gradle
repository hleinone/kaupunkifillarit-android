apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "io.fabric"
apply plugin: "kotlinx-serialization"

task askForPasswords << {
    android.signingConfigs.release.storeFile = file(STORE)
    android.signingConfigs.release.keyAlias = ALIAS
    if (project.hasProperty("STORE_PASSWORD")) {
        android.signingConfigs.release.storePassword = STORE_PASSWORD
    } else {
        android.signingConfigs.release.storePassword = new String(System.console().readPassword("\n\nKeystore " + android.signingConfigs.release.storeFile + " password: "))
    }
    if (project.hasProperty("KEY_PASSWORD")) {
        android.signingConfigs.release.keyPassword = KEY_PASSWORD
    } else {
        android.signingConfigs.release.keyPassword = new String(System.console().readPassword("\nKey " + android.signingConfigs.release.keyAlias + " password: "))
    }
}

tasks.withType(com.android.build.gradle.tasks.PackageApplication).whenTaskAdded { task ->
    if (!task.name.endsWith("Debug") && !task.name.endsWith("Test")) {
        task.dependsOn "askForPasswords"
    }
}

android {
    compileSdkVersion 27

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file("version.properties")))
    def code = System.getenv().BUILD_NUMBER ?: versionProps["code"]
    defaultConfig {
        applicationId "fi.kaupunkifillarit"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode code.toInteger()
        versionName versionProps["name"]
    }

    signingConfigs {
        release {
            storeFile file("/dev/null")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        debug.java.srcDirs += "src/debug/kotlin"
    }

    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/NOTICE.txt"
        exclude "META-INF/app_debug.kotlin_module"
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "com.google.android.gms:play-services-maps:$gms_version"
    implementation "com.google.android.gms:play-services-location:$gms_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    //implementation "com.google.firebase:firebase-core:15.0.2"

    implementation "io.reactivex.rxjava2:rxkotlin:2.1.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation "com.trello.rxlifecycle2:rxlifecycle:$rxlifecycle_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-android:$rxlifecycle_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-components:$rxlifecycle_version"
    implementation "com.trello.rxlifecycle2:rxlifecycle-kotlin:$rxlifecycle_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.0.0"
    implementation "com.patloew.rxlocation:rxlocation:1.0.3"
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:0.9.5"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel_version"
    implementation "com.github.kittinunf.fuel:fuel-rxjava:$fuel_version"

    implementation "com.jakewharton.timber:timber:4.5.1"
    implementation "com.crashlytics.sdk.android:crashlytics:2.9.2"
    implementation "uk.co.chrisjenx:calligraphy:2.2.0"
    implementation "de.psdev.licensesdialog:licensesdialog:1.8.2"
}

apply plugin: "com.google.gms.google-services"
