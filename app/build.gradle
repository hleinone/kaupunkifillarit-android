apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'io.fabric'
apply plugin: 'build-time-tracker'

task askForPasswords << {
    android.signingConfigs.release.storeFile = file(STORE)
    android.signingConfigs.release.keyAlias = ALIAS
    if (project.hasProperty('STORE_PASSWORD')) {
        android.signingConfigs.release.storePassword = STORE_PASSWORD
    } else {
        android.signingConfigs.release.storePassword = new String(System.console().readPassword('\n\nKeystore ' + android.signingConfigs.release.storeFile + ' password: '))
    }
    if (project.hasProperty('KEY_PASSWORD')) {
        android.signingConfigs.release.keyPassword = KEY_PASSWORD
    } else {
        android.signingConfigs.release.keyPassword = new String(System.console().readPassword('\nKey ' + android.signingConfigs.release.keyAlias + ' password: '))
    }
}

tasks.withType(com.android.build.gradle.tasks.PackageApplication).whenTaskAdded { task ->
    if (!task.name.endsWith('Debug') && !task.name.endsWith('Test')) {
        task.dependsOn 'askForPasswords'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))
    def code = System.getenv().BUILD_NUMBER ?: versionProps['code']
    defaultConfig {
        applicationId 'fi.kaupunkifillarit'
        minSdkVersion 17
        targetSdkVersion 23
        versionCode code.toInteger()
        versionName versionProps['name']
        //multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file('/dev/null')
            storePassword ''
            keyAlias ''
            keyPassword ''
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
            versionNameSuffix ' debug'
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    //compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.android.gms:play-services-maps:8.4.0'
    compile 'com.google.android.gms:play-services-location:8.4.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.google.dagger:dagger:2.4'
    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.jakewharton:butterknife:8.0.1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
    compile 'org.functionaljava:functionaljava:4.3'
    compile 'io.reactivex:rxjava:1.1.5'
    compile 'io.reactivex:rxandroid:1.2.0'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    compile 'pl.charmas.android:android-reactive-location:0.8@aar'
    compile 'com.squareup.okhttp:okhttp:2.7.5'
    compile 'com.squareup.okio:okio:1.8.0'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.7.4'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }
//    debugCompile 'com.facebook.stetho:stetho:1.3.1'
//    debugCompile 'com.facebook.stetho:stetho-okhttp:1.3.1'
    provided 'javax.annotation:jsr250-api:1.0'
}

buildtimetracker {
    reporters {
        csv {
            output 'build/times.csv'
            append true
            header false
        }

        summary {
            ordered false
            threshold 50
            barstyle 'unicode'
        }

        csvSummary {
            csv 'build/times.csv'
        }
    }
}

apply plugin: 'com.google.gms.google-services'
