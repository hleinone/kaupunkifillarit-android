plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

task askForPasswords {
    doLast {
        android.signingConfigs.release.storeFile = file(STORE)
        android.signingConfigs.release.keyAlias = ALIAS
        if (project.hasProperty("STORE_PASSWORD")) {
            android.signingConfigs.release.storePassword = STORE_PASSWORD
        } else {
            android.signingConfigs.release.storePassword = new String(System.console().readPassword("\n\nKeystore " + android.signingConfigs.release.storeFile + " password: "))
        }
        if (project.hasProperty("KEY_PASSWORD")) {
            android.signingConfigs.release.keyPassword = KEY_PASSWORD
        } else {
            android.signingConfigs.release.keyPassword = new String(System.console().readPassword("\nKey " + android.signingConfigs.release.keyAlias + " password: "))
        }
    }
}

tasks.whenTaskAdded { task ->
    if (task.name.trim() in ["validateSigningRelease", "signingReport"]) {
        task.dependsOn askForPasswords
    }
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "fi.kaupunkifillarit"
        minSdkVersion 17
        targetSdkVersion 30
        versionCode 18
        versionName "2.0.0"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file("/dev/null")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.activity:activity-ktx:1.2.1'
    implementation 'androidx.fragment:fragment-ktx:1.3.1'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.android.gms:play-services-maps:17.0.0'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    implementation 'com.google.firebase:firebase-crashlytics:17.4.0'
    implementation 'com.google.firebase:firebase-analytics:18.0.2'

    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.1.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'

    implementation 'com.github.kittinunf.fuel:fuel:2.3.1'
    implementation 'com.github.kittinunf.fuel:fuel-coroutines:2.3.1'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'
}