apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "io.fabric"
apply plugin: "build-time-tracker"

task askForPasswords << {
    android.signingConfigs.release.storeFile = file(STORE)
    android.signingConfigs.release.keyAlias = ALIAS
    if (project.hasProperty("STORE_PASSWORD")) {
        android.signingConfigs.release.storePassword = STORE_PASSWORD
    } else {
        android.signingConfigs.release.storePassword = new String(System.console().readPassword("\n\nKeystore " + android.signingConfigs.release.storeFile + " password: "))
    }
    if (project.hasProperty("KEY_PASSWORD")) {
        android.signingConfigs.release.keyPassword = KEY_PASSWORD
    } else {
        android.signingConfigs.release.keyPassword = new String(System.console().readPassword("\nKey " + android.signingConfigs.release.keyAlias + " password: "))
    }
}

tasks.withType(com.android.build.gradle.tasks.PackageApplication).whenTaskAdded { task ->
    if (!task.name.endsWith("Debug") && !task.name.endsWith("Test")) {
        task.dependsOn "askForPasswords"
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file("version.properties")))
    def code = System.getenv().BUILD_NUMBER ?: versionProps["code"]
    defaultConfig {
        applicationId "fi.kaupunkifillarit"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode code.toInteger()
        versionName versionProps["name"]
    }

    signingConfigs {
        release {
            storeFile file("/dev/null")
            storePassword ""
            keyAlias ""
            keyPassword ""
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix " debug"
        }

        release {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        debug.java.srcDirs += "src/debug/kotlin"
        //androidTest.java.srcDirs += "src/androidTest/kotlin"
        //test.java.srcDirs += "src/test/kotlin"
    }

    packagingOptions {
        exclude "META-INF/LICENSE"
        exclude "META-INF/LICENSE.txt"
        exclude "META-INF/NOTICE"
        exclude "META-INF/NOTICE.txt"
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.google.android.gms:play-services-maps:11.0.1"
    compile "com.google.android.gms:play-services-location:11.0.1"
    compile "com.google.android.gms:play-services-analytics:11.0.1"
    compile "com.android.support:appcompat-v7:25.3.1"
    compile "com.android.support:design:25.3.1"

    compile "com.bluelinelabs:logansquare:1.3.7"
    kapt "com.bluelinelabs:logansquare-compiler:1.3.7"

    compile "io.reactivex.rxjava2:rxkotlin:2.1.0"
    compile "io.reactivex.rxjava2:rxandroid:2.0.1"
    compile "com.trello.rxlifecycle2:rxlifecycle:2.1.0"
    compile "com.trello.rxlifecycle2:rxlifecycle-android:2.1.0"
    compile "com.trello.rxlifecycle2:rxlifecycle-components:2.1.0"
    compile "com.trello.rxlifecycle2:rxlifecycle-kotlin:2.1.0"
    compile "com.jakewharton.rxbinding2:rxbinding-kotlin:2.0.0"
    compile "com.jakewharton.rxbinding2:rxbinding-support-v4-kotlin:2.0.0"
    compile "com.patloew.rxlocation:rxlocation:1.0.3"

    compile "com.github.kittinunf.fuel:fuel:1.9.0"
    compile "com.github.kittinunf.fuel:fuel-android:1.9.0"
    compile "com.github.kittinunf.fuel:fuel-rxjava:1.9.0"

    compile "com.jakewharton.timber:timber:4.5.1"
    compile "com.crashlytics.sdk.android:crashlytics:2.6.8"
    compile "com.crashlytics.sdk.android:answers:1.3.13"
    compile "uk.co.chrisjenx:calligraphy:2.2.0"
    compile "de.psdev.licensesdialog:licensesdialog:1.8.2"
}

kapt {
    generateStubs = true
}

buildtimetracker {
    reporters {
        csv {
            output "build/times.csv"
            append true
            header false
        }

        summary {
            ordered false
            threshold 50
            barstyle "unicode"
        }

        csvSummary {
            csv "build/times.csv"
        }
    }
}

buildscript {
    ext.kotlin_version = "1.1.2-5"

    repositories {
        jcenter()
        maven { url "https://maven.fabric.io/public" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "com.google.gms.google-services"
repositories {
    mavenCentral()
}
